#Область ПрограммныйИнтерфейс

Процедура ДополнитьФорму(Форма) Экспорт 

	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда  
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма); 
		ДобавитьПолеСогласованнаяСкидкаИКнопкуПересчета(Форма); 
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровИУслуг.Форма.ФормаДокумента"
			  ИЛИ ИмяФормы = "Документ.РеализацияТоваровИУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
	    ВставитьПолеКонтактноеЛицоПередОснование(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоПередСуммаДокумента(Форма);
	КонецЕсли;
	
КонецПроцедуры 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)
	
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДЗ_КонтактноеЛицо";
	
КонецПроцедуры

Процедура ВставитьПолеКонтактноеЛицоПередОснование(Форма)
	
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.Основание);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДЗ_КонтактноеЛицо";
	
КонецПроцедуры

Процедура ВставитьПолеКонтактноеЛицоПередСуммаДокумента(Форма)
	
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.СуммаДокумента);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДЗ_КонтактноеЛицо";
	
КонецПроцедуры

Процедура ДобавитьПолеСогласованнаяСкидкаИКнопкуПересчета(Форма)
	
	ГруппаСкидка = Форма.Элементы.Добавить("ГруппаСкидка", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);
	ГруппаСкидка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСкидка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	ГруппаСкидка.ОтображатьЗаголовок = Ложь;
		
	ПолеФормы = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаСкидка);
	ПолеФормы.Вид = ВидПоляФормы.ПолеВвода;
	ПолеФормы.ПутьКДанным = "Объект.ДЗ_СогласованнаяСкидка"; 
	ПолеФормы.УстановитьДействие("ПриИзменении", "ПриИзмененииСогласованнаяСкидка");
	
	Команда = Форма.Команды.Добавить("ПересчитатьТаблицу");
	Команда.Действие = "ПересчитатьТаблицу";
		
	Кнопка = Форма.Элементы.Добавить("ПересчитатьТаблицу", Тип("КнопкаФормы"), Форма.Элементы.ГруппаСкидка);
	Кнопка.Вид = ВидКнопкиФормы.ОбычнаяКнопка;  
	Кнопка.Заголовок = "Пересчитать таблицу";
	Кнопка.ИмяКоманды = "ПересчитатьТаблицу";
	
КонецПроцедуры

#КонецОбласти
	

     

